.include "lab2checkoff.jsim"
.include "nominal.jsim"

* 2-input nand: inputs are a and b, output is z
.subckt nand2 a b z
MPD1 z a 1 0 NENH sw=8 sl=1
MPD2 1 b 0 0 NENH sw=8 sl=1
MPU1 z a vdd vdd PENH sw=9 sl=1
MPU2 z b vdd vdd PENH sw=9 sl=1
.ends

* inverter: input is a, output is z
.subckt inv a z
MPD1 z a 0 0 NENH sw=16 sl=1
MPU1 z a vdd vdd PENH sw=16 sl=1
.ends

* 2-input nor: inputs are a and b, output is z
.subckt nor2 a b z
MPD1 z a 0 0 NENH sw=8 sl=1
MPD2 z b 0 0 NENH sw=8 sl=1
MPU1 z a 1 vdd PENH sw=9 sl=1
MPU2 1 b vdd vdd PENH sw=9 sl=1
.ends

* 2-input xor: input is a and b, output is z
.subckt xor2 a b z
* Build your own xor gate here
* BEGIN ANSWER
XINVA a ai inv
XINVB b bi inv
XNAND1 ai b aib nand2
XNAND2 a bi abi nand2
XNANDF aib abi z nand2

*END ANSWER
.ends

* 2-input xnor: input is a and b, output is z
.subckt xnor2 a b z
* Build your own xnor gate here
* BEGIN ANSWER
XINVA a ai inv
XINVB b bi inv
XNAND1 ai b aib nand2
XNAND2 a bi abi nand2
XNANDF aib abi z_i nand2
XINVF z_i z inv

*END ANSWER
.ends


* FA: 1-bit Full Adder circuit
* Input: a, b, ci 
* Output: s, co
.subckt FA a b ci s co
* write your FA circuitry here
* BEGIN ANSWER
* Co Circuit
XNANDAB a b ab nand2
XNANDBC b ci bc nand2
XNANDAC a ci ac nand2
XINVAC ac ac_i inv
XNANDABBC ab bc abbc nand2
XNORABC abbc ac_i co_i nor2
XINVABC co_i co inv 

* S Circuit
XXORAB a b ab_xor xor2
XXORABC ab_xor ci s xor2

*END ANSWER
.ends


.subckt ADDER4 a3 a2 a1 a0 b3 b2 b1 b0 s4 s3 s2 s1 s0
Xbit0 a0 b0 0 s0 c0 FA
Xbit1 a1 b1 c0 s1 c1 FA
Xbit2 a2 b2 c1 s2 c2 FA
Xbit3 a3 b3 c2 s3 s4 FA
.ends

